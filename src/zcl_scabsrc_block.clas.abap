class ZCL_SCABSRC_BLOCK definition
  public
  create public .

public section.

  interfaces ZIF_SCABSRC_BLOCK .

  aliases GET_ALL_FIELDS
    for ZIF_SCABSRC_BLOCK~GET_ALL_FIELDS .
  aliases GET_CHILD_BLOCKS
    for ZIF_SCABSRC_BLOCK~GET_CHILD_BLOCKS .
  aliases GET_FIRST_STATEMENT
    for ZIF_SCABSRC_BLOCK~GET_FIRST_STATEMENT .
  aliases GET_INDEX
    for ZIF_SCABSRC_BLOCK~GET_INDEX .
  aliases GET_PARENT_BLOCK
    for ZIF_SCABSRC_BLOCK~GET_PARENT_BLOCK .
  aliases GET_STATEMENTS
    for ZIF_SCABSRC_BLOCK~GET_STATEMENTS .
  aliases GET_STMNT_TYPE
    for ZIF_SCABSRC_BLOCK~GET_STMNT_TYPE .
  aliases GET_TYPE
    for ZIF_SCABSRC_BLOCK~GET_TYPE .

  methods CONSTRUCTOR
    importing
      !SCABSRC type ref to ZCL_SCABSRC
      !INDEX type I .
protected section.
private section.

  data INDEX type SYTABIX .
  data SCABSRC type ref to ZCL_SCABSRC .
  data TEMP type SSTRUC .
ENDCLASS.



CLASS ZCL_SCABSRC_BLOCK IMPLEMENTATION.


  method CONSTRUCTOR.

    ME->INDEX = INDEX.
    ME->SCABSRC = SCABSRC.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_ALL_FIELDS.

    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX INTO BLOCK.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_CHILD_BLOCKS.

    FIELD-SYMBOLS <LS_SSTRUC> TYPE SSTRUC.
    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX ASSIGNING <LS_SSTRUC>.
    IF SY-SUBRC = 0.
      CREATE OBJECT BLOCKS TYPE ZCL_SCABSRC_BLOCKS
        EXPORTING
          SCABSRC = SCABSRC
          FROM    = <LS_SSTRUC>-STRUC_FROM
          TO      = <LS_SSTRUC>-STRUC_TO.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_FIRST_STATEMENT.

    FIELD-SYMBOLS <LS_SSTRUC> TYPE SSTRUC.
    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX ASSIGNING <LS_SSTRUC>.
    IF SY-SUBRC = 0.
      CREATE OBJECT STATEMENT TYPE ZCL_SCABSRC_STATEMENT
        EXPORTING
          SCABSRC = SCABSRC
          INDEX   = <LS_SSTRUC>-STMNT_FROM.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_INDEX.

    INDEX = ME->INDEX.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_PARENT_BLOCK.

    FIELD-SYMBOLS <LS_SSTRUC> TYPE SSTRUC.
    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX ASSIGNING <LS_SSTRUC>.
    IF SY-SUBRC = 0.
      CREATE OBJECT BLOCK TYPE ZCL_SCABSRC_BLOCK
        EXPORTING
          SCABSRC = SCABSRC
          INDEX   = <LS_SSTRUC>-BACK.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_STATEMENTS.

    FIELD-SYMBOLS <LS_SSTRUC> TYPE SSTRUC.
    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX ASSIGNING <LS_SSTRUC>.
    IF SY-SUBRC = 0.
      CREATE OBJECT STATEMENTS TYPE ZCL_SCABSRC_STATEMENTS
        EXPORTING
          SCABSRC = SCABSRC
          FROM    = <LS_SSTRUC>-STMNT_FROM
          TO      = <LS_SSTRUC>-STMNT_TO.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_STMNT_TYPE.

    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX INTO TEMP TRANSPORTING STMNT_TYPE.
    STMNT_TYPE = TEMP-STMNT_TYPE.

  endmethod.


  method ZIF_SCABSRC_BLOCK~GET_TYPE.

    READ TABLE SCABSRC->LT_SSTRUC INDEX INDEX INTO TEMP TRANSPORTING TYPE.
    TYPE = TEMP-TYPE.

  endmethod.



ENDCLASS.
