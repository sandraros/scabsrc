class ZCL_SCABSRC_STATEMENT definition
  public
  create public .

public section.

  interfaces ZIF_SCABSRC_STATEMENT .

  aliases GET_ALL_FIELDS
    for ZIF_SCABSRC_STATEMENT~GET_ALL_FIELDS .
  aliases GET_BLOCK
    for ZIF_SCABSRC_STATEMENT~GET_BLOCK .
  aliases GET_BLOCKS
    for ZIF_SCABSRC_STATEMENT~GET_BLOCKS .
  aliases GET_INDEX
    for ZIF_SCABSRC_STATEMENT~GET_INDEX .
  aliases GET_SOURCE_UNIT
    for ZIF_SCABSRC_STATEMENT~GET_SOURCE_UNIT .
  aliases GET_TOKENS
    for ZIF_SCABSRC_STATEMENT~GET_TOKENS .

  methods CONSTRUCTOR
    importing
      !SCABSRC type ref to ZCL_SCABSRC
      !INDEX type I .
protected section.
private section.

  data INDEX type SYTABIX .
  data SCABSRC type ref to ZCL_SCABSRC .
ENDCLASS.



CLASS ZCL_SCABSRC_STATEMENT IMPLEMENTATION.


  method CONSTRUCTOR.

    ME->INDEX = INDEX.
    ME->SCABSRC = SCABSRC.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_ALL_FIELDS.

    READ TABLE SCABSRC->LT_SSTMNT INDEX INDEX INTO STATEMENT.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_BLOCK.

    FIELD-SYMBOLS <LS_SSTMNT> TYPE SSTMNT.
    READ TABLE SCABSRC->LT_SSTMNT INDEX INDEX ASSIGNING <LS_SSTMNT>.
    IF SY-SUBRC = 0.
      CREATE OBJECT BLOCK TYPE ZCL_SCABSRC_BLOCK
        EXPORTING
          SCABSRC = SCABSRC
          INDEX   = <LS_SSTMNT>-STRUC.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_BLOCKS.

    DATA LO_BLOCK TYPE REF TO ZIF_SCABSRC_BLOCK.

    LO_BLOCK = GET_BLOCK( ).
    IF LO_BLOCK IS BOUND.
      CREATE OBJECT BLOCKS TYPE ZCL_SCABSRC_BLOCKS
        EXPORTING
          SCABSRC = SCABSRC.
    ENDIF.
    WHILE LO_BLOCK IS BOUND.
      BLOCKS->ADD_BLOCK( LO_BLOCK->GET_INDEX( ) ).
      IF ( TYPE IS SUPPLIED AND TYPE = LO_BLOCK->GET_TYPE( ) )
            OR ( STMNT_TYPE IS SUPPLIED AND STMNT_TYPE = LO_BLOCK->GET_STMNT_TYPE( ) ).
        EXIT.
      ENDIF.
      LO_BLOCK = LO_BLOCK->GET_PARENT_BLOCK( ).
    ENDWHILE.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_INDEX.

    INDEX = ME->INDEX.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_SOURCE_UNIT.

    FIELD-SYMBOLS <LS_SSTMNT> TYPE SSTMNT.
    READ TABLE SCABSRC->LT_SSTMNT INDEX INDEX ASSIGNING <LS_SSTMNT>.
    IF SY-SUBRC = 0.
      CREATE OBJECT SOURCE_UNIT TYPE ZCL_SCABSRC_SOURCE_UNIT
        EXPORTING
          SCABSRC = SCABSRC
          INDEX   = <LS_SSTMNT>-LEVEL.
    ENDIF.

  endmethod.


  method ZIF_SCABSRC_STATEMENT~GET_TOKENS.

    FIELD-SYMBOLS <LS_SSTMNT> TYPE SSTMNT.
    READ TABLE SCABSRC->LT_SSTMNT INDEX INDEX ASSIGNING <LS_SSTMNT>.
    IF SY-SUBRC = 0.
      CREATE OBJECT TOKENS TYPE ZCL_SCABSRC_TOKENS
        EXPORTING
          SCABSRC = SCABSRC
          FROM    = <LS_SSTMNT>-FROM
          TO      = <LS_SSTMNT>-TO.
    ENDIF.

  endmethod.


ENDCLASS.
